{ stdenv, fetchFromGitHub, fetchpatch, pkgconfig, intltool, gperf, libcap, kmod
, zlib, xz, pam, acl, cryptsetup, libuuid, m4, utillinux, libffi
, glib, kbd, libxslt, coreutils, libgcrypt, libgpgerror, libidn2, libapparmor
, audit, lz4, kexectools, libmicrohttpd, linuxHeaders ? stdenv.cc.libc.linuxHeaders
, libseccomp, iptables, gnu-efi
, autoreconfHook, gettext, docbook_xsl, docbook_xml_dtd_42, docbook_xml_dtd_45
, ninja, meson, python3Packages, glibcLocales
}:

assert stdenv.isLinux;

let pythonLxmlEnv = python3Packages.python.withPackages ( ps: with ps; [ python3Packages.lxml ]);

in

  stdenv.mkDerivation rec {
    version = "235";
    name = "systemd-${version}";

    src = fetchFromGitHub {
      owner = "flokli";
      repo = "systemd";
      rev = "nixos-upstream-v235";
      sha256 = "0358854l9hri3kcrdjjma9cczb4v2sqx8cxb84g4kpkb465hf9f7";
    };


    outputs = [ "out" "lib" "man" "dev" ];

    nativeBuildInputs =
      [ pkgconfig intltool gperf libxslt gettext docbook_xsl docbook_xml_dtd_42 docbook_xml_dtd_45
        ninja meson
        coreutils # meson calls date, stat etc.
        #makeWrapper
        #python3Packages.python python3Packages.lxml
        pythonLxmlEnv glibcLocales
      ];
    buildInputs =
      [ linuxHeaders libcap kmod xz pam acl
        /* cryptsetup */ libuuid m4 glib libgcrypt libgpgerror libidn2
        libmicrohttpd kexectools libseccomp libffi audit lz4 libapparmor
        iptables gnu-efi
      ];

    #dontAddPrefix = true;

    mesonFlags = [
      #"-Dprefix=$(out)"
      "-Dloadkeys-path=${kbd}/bin/loadkeys"
      "-Dsetfont-path=${kbd}/bin/setfont"
      "-Ddbuspolicydir=$(out)/etc/dbus-1/system.d"
      "-Ddbussessionservicedir=$(out)/share/dbus-1/services"
      "-Ddbussystemservicedir=$(out)/share/dbus-1/system-services"
      "-Dpamconfdir=$(out)/etc/pam.d"
      "-Dsysconfdir=$(out)/etc"
      "-Dtty-gid=3" # tty in NixOS has gid 3
  #    "-Dtests=" # TODO
      "-Dlz4=true"
      "-Dhostnamed=true"
      "-Dnetworkd=true"
      "-Dsysusers=false"
      "-Dtimedated=true"
      "-Dtimesyncd=false"
      "-Dfirstboot=false"
      "-Dlocaled=true"
      "-Dresolve=true"
      "-Dsplit-usr=false"
      "-Dlibcurl=false"
      "-Dlibidn=false"
      "-Dlibidn2=true" #TODO: document this might have been activated now
      "-Dquotacheck=false"
      "-Dldconfig=false"
      "-Dsmack=false"
      "-Dsystem-uid-max=499" #TODO: debug why awking around in /etc/login.defs doesn't work
      "-Dsystem-gid-max=499"
  #    "-Dtime-epoch=1"

      (if stdenv.isArm then "-Dgnu-efi=false" else "-Dgnu-efi=true")
      "-Defi-libdir=${gnu-efi}/lib"
      "-Defi-includedir=${gnu-efi}/include/efi"
      "-Defi-ldsdir=${gnu-efi}/lib"

      "-Dsysvinit-path="
      "-Dsysvrcnd-path="
  #    "-Dntp-servers='0.nixos.pool.ntp.org 1.nixos.pool.ntp.org 2.nixos.pool.ntp.org 3.nixos.pool.ntp.org'"
      ];

    hardeningDisable = [ "stackprotector" ];

    preConfigure =
      ''
        mesonFlags+=("-Dntp-servers=0.nixos.pool.ntp.org 1.nixos.pool.ntp.org 2.nixos.pool.ntp.org 3.nixos.pool.ntp.org")
        export LC_ALL="en_US.UTF-8";
        # FIXME: patch this in systemd properly (and send upstream).
        # already fixed in f00929ad622c978f8ad83590a15a765b4beecac9: (u)mount
        for i in src/remount-fs/remount-fs.c src/core/mount.c src/core/swap.c src/fsck/fsck.c units/emergency.service.in units/rescue.service.in src/journal/cat.c src/core/shutdown.c src/nspawn/nspawn.c src/shared/generator.c; do
          test -e $i
          substituteInPlace $i \
            --replace /usr/bin/getent ${stdenv.glibc.bin}/bin/getent \
            --replace /sbin/swapon ${utillinux.bin}/sbin/swapon \
            --replace /sbin/swapoff ${utillinux.bin}/sbin/swapoff \
            --replace /sbin/fsck ${utillinux.bin}/sbin/fsck \
            --replace /bin/echo ${coreutils}/bin/echo \
            --replace /bin/cat ${coreutils}/bin/cat \
            --replace /sbin/sulogin ${utillinux.bin}/sbin/sulogin \
            --replace /usr/lib/systemd/systemd-fsck $out/lib/systemd/systemd-fsck \
            --replace /bin/plymouth /run/current-system/sw/bin/plymouth # To avoid dependency
        done

        for i in tools/xml_helper.py tools/make-directive-index.py tools/make-man-index.py test/sys-script.py; do
          substituteInPlace $i \
            --replace "#!/usr/bin/env python" "#!${pythonLxmlEnv}/bin/python"
        done

        for i in src/basic/generate-gperfs.py src/resolve/generate-dns_type-gperf.py src/test/generate-sym-test.py ; do
          substituteInPlace $i \
            --replace "#!/usr/bin/env python" "#!${python3Packages.python}/bin/python"
        done

        substituteInPlace src/journal/catalog.c \
          --replace /usr/lib/systemd/catalog/ $out/lib/systemd/catalog/
      '';

    PYTHON_BINARY = "${pythonLxmlEnv}/bin/python"; # don't want a build time dependency on Python

    NIX_CFLAGS_COMPILE =
      [ # Can't say ${polkit.bin}/bin/pkttyagent here because that would
        # lead to a cyclic dependency.
        "-UPOLKIT_AGENT_BINARY_PATH" "-DPOLKIT_AGENT_BINARY_PATH=\"/run/current-system/sw/bin/pkttyagent\""
        "-fno-stack-protector"

        # Set the release_agent on /sys/fs/cgroup/systemd to the
        # currently running systemd (/run/current-system/systemd) so
        # that we don't use an obsolete/garbage-collected release agent.
        "-USYSTEMD_CGROUP_AGENT_PATH" "-DSYSTEMD_CGROUP_AGENT_PATH=\"/run/current-system/systemd/lib/systemd/systemd-cgroups-agent\""

        "-USYSTEMD_BINARY_PATH" "-DSYSTEMD_BINARY_PATH=\"/run/current-system/systemd/lib/systemd/systemd\""
      ];

#    installFlags =
#      [ "localstatedir=$(TMPDIR)/var"
#        "sysvinitdir=$(TMPDIR)/etc/init.d"
#      ];
    installFlags = "DESTDIR=$(out) PREFIX=";

    postInstall =
      ''
        # sysinit.target: Don't depend on
        # systemd-tmpfiles-setup.service. This interferes with NixOps's
        # send-keys feature (since sshd.service depends indirectly on
        # sysinit.target).
        mv $out/lib/systemd/system/sysinit.target.wants/systemd-tmpfiles-setup-dev.service $out/lib/systemd/system/multi-user.target.wants/

        mkdir -p $out/example/systemd
        mv $out/lib/{modules-load.d,binfmt.d,sysctl.d,tmpfiles.d} $out/example
        mv $out/lib/systemd/{system,user} $out/example/systemd

        rm -rf $out/etc/systemd/system

        # Install SysV compatibility commands.
        mkdir -p $out/sbin
        ln -s $out/lib/systemd/systemd $out/sbin/telinit
        for i in init halt poweroff runlevel reboot shutdown; do
          ln -s $out/bin/systemctl $out/sbin/$i
        done

        # Fix reference to /bin/false in the D-Bus services.
        for i in $out/share/dbus-1/system-services/*.service; do
          substituteInPlace $i --replace /bin/false ${coreutils}/bin/false
        done

        rm -rf $out/etc/rpm

        rm $lib/lib/*.la

        # "kernel-install" shouldn't be used on NixOS.
        find $out -name "*kernel-install*" -exec rm {} \;

        # Keep only libudev and libsystemd in the lib output.
        mkdir -p $out/lib
        mv $lib/lib/security $lib/lib/libnss* $out/lib/
      ''; # */

    enableParallelBuilding = false;

    # The interface version prevents NixOS from switching to an
    # incompatible systemd at runtime.  (Switching across reboots is
    # fine, of course.)  It should be increased whenever systemd changes
    # in a backwards-incompatible way.  If the interface version of two
    # systemd builds is the same, then we can switch between them at
    # runtime; otherwise we can't and we need to reboot.
    passthru.interfaceVersion = 2;

    meta = {
      homepage = http://www.freedesktop.org/wiki/Software/systemd;
      description = "A system and service manager for Linux";
      platforms = stdenv.lib.platforms.linux;
      maintainers = [ stdenv.lib.maintainers.eelco ];
    };
}
